plugins {
    id("io.github.gradle-nexus.publish-plugin")
}

ext {
    kotlin_version = '1.7.10'
    kotlin_coroutine_version = '1.6.4'
    lifecycle_scope_version = '2.3.1'

    // Android libraries
    appcompat_version = '1.5.1'
    corektx_version = '1.9.0'
    annotation_version = '1.4.0'
    androidx_crypto_version = "1.0.0"
    tink_version = "1.7.0"
    material_version = "1.6.1"

    // Test
    junit_version = '4.13.2'
    test_runner_version = '1.4.0'
    espresso_core_version = '3.4.0'
    espresso_remote_version = '3.4.0'
    test_ext_version = '1.1.3'
    test_orchestrator_version = '1.4.1'
    test_rules_version = '1.4.0'

    // Android project
    harmony_minSdk_version = 17
    crypto_minSdk_version = 23
    compileSdk_version = 33
    targetSdk_version = 33
    nav_version = "2.5.2"
    constraint_version = '2.1.4'
    hilt_version = '2.43.2'
    room_version = "2.4.3"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["profileId"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    println "Found secret props file, loading props"
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    println "No props file, loading env vars"
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
    ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
    ext["ossrhUsername"] = System.getenv('MAVEN_USERNAME')
    ext["ossrhPassword"] = System.getenv('MAVEN_PASSWORD')
    ext["profileId"] = System.getenv('STAGING_PROFILE_ID')
}

nexusPublishing {
    packageGroup = 'com.frybits.harmony'
    repositories {
        sonatype {
            stagingProfileId = profileId
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}
